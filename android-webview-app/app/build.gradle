plugins {
    id 'com.android.application'
}

// Load keystore properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'app.zaptrax'
    compileSdk 34

    defaultConfig {
        applicationId "app.zaptrax"
        minSdk 24
        targetSdk 34
        versionCode 7
        versionName "1.0.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['storeFile'] ?: System.getProperty("user.home") + "/.android-keys/zaptrax-keystore.jks")
            storePassword keystoreProperties['storePassword'] ?: System.getenv("ZAPTRAX_KEYSTORE_PASSWORD")
            keyAlias keystoreProperties['keyAlias'] ?: "zaptrax-key"
            keyPassword keystoreProperties['keyPassword'] ?: System.getenv("ZAPTRAX_KEYSTORE_PASSWORD")

            // Enable both V1 and V2 signing for compatibility
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.webkit:webkit:1.9.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.media:media:1.7.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
